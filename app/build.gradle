apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        releaseConfig {
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
            storeFile rootProject.ext.storeFile
            storePassword rootProject.ext.storePassword
        }
    }

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion 21
        targetSdkVersion 29
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BUILD_TIME", "\"${new Date().getTime()}\""
        buildConfigField "String", "APPLICATION_KEY", "\"${rootProject.ext.applicationKey}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    compileOptions {
        // support new language apis
        coreLibraryDesugaringEnabled true

        // java compatibility to java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    flavorDimensions "version"

    productFlavors {
        development {
            dimension "version"
            applicationIdSuffix ".development"
            versionNameSuffix "-development"

            resValue "string", "app_name", "\"${rootProject.ext.development.appName}\""
            resValue "integer", "config_fetch_interval", "${rootProject.ext.development.configFetchInterval}"

            buildConfigField "String", "BASE_SERVER_URL", "\"${rootProject.ext.development.baseServerURL}\""
        }

        staging {
            dimension "version"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"

            resValue "string", "app_name", "\"${rootProject.ext.staging.appName}\""
            resValue "integer", "config_fetch_interval", "${rootProject.ext.staging.configFetchInterval}"

            buildConfigField "String", "BASE_SERVER_URL", "\"${rootProject.ext.staging.baseServerURL}\""
        }

        production {
            dimension "version"

            resValue "string", "app_name", "\"${rootProject.ext.production.appName}\""
            resValue "integer", "config_fetch_interval", "${rootProject.ext.production.configFetchInterval}"

            buildConfigField "String", "BASE_SERVER_URL", "\"${rootProject.ext.production.baseServerURL}\""
        }
    }
}

dependencies {
    // desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.5'

    // libs
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'

    // appcompat
    implementation 'androidx.appcompat:appcompat:1.1.0'

    // constraint layout
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}